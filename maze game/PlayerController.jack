class PlayerController {
	field Player player;
	field Array maze;
	field int blockWidth, blockHeight, xCount, yCount, drawPadding, baseX, baseY;

	constructor PlayerController new(Player newPlayer, Array newMaze, int xOrigin, int yOrigin,
		int bw, int bh, int xc, int yc, int pad){
		var int x, y;
		let player = newPlayer;
		let maze = newMaze;
		let blockWidth = bw;
		let blockHeight = bh;
		let xCount = xc;
		let yCount = yc;
		let drawPadding = pad;
		let baseX = xOrigin;
		let baseY = yOrigin;

		let x = player.getX();
		let y = player.getY();	



		do Screen.setColor(true);
		do Screen.drawRectangle(baseX + (x * blockWidth) + drawPadding, 
			baseY + (y * blockHeight) + drawPadding,
			baseX + ((x+1) * blockWidth) - drawPadding,
			baseY + ((y+1) * blockHeight) - drawPadding );

		return this;
	}

	method void loop(){
		var int direction, x, y;
		var char key;
		let key = 0;
		let x = player.getX();
		let y = player.getY();

		
		while( ~((x = (xCount - 1)) & (y = 0))){ // didn't reach the target
			while((key < 130) | (key > 133)){ //didn't click on one the arrows
				let key = Keyboard.keyPressed();
			}

			if(key = 130){ //left
				let direction = 8;
			}	
			
			if(key = 131){ //up
				let direction = 1;
			}	
			
			if(key = 132){ //right
				let direction = 4;
			}	
			
			if(key = 133){ //down
				let direction = 2;
			}

			if(canPlayerGo(direction)){
				let x = player.getX();
				let y = player.getY();				
				//first remove player from it current place
				do Screen.setColor(false);
				do Screen.drawRectangle(baseX + (x * blockWidth) + drawPadding, 
										baseY + (y * blockHeight) + drawPadding,
										baseX + ((x+1) * blockWidth) - drawPadding,
										baseY + ((y+1) * blockHeight) - drawPadding );

				do player.go(direction);

				let x = player.getX();
				let y = player.getY();	
				do Screen.setColor(true);
				do Screen.drawRectangle(baseX + (x * blockWidth) + drawPadding, 
										baseY + (y * blockHeight) + drawPadding,
										baseX + ((x+1) * blockWidth) - drawPadding,
										baseY + ((y+1) * blockHeight) - drawPadding );

				}

				let key = 0;
				do Sys.wait(200);
		}

		

		return;
	}
	// up -1
	// down -2
	// right -4
	// left- 8

	method boolean canPlayerGo(int direction){
		var int x, y, val;

		var Array tempArr;
		let x = player.getX();
		let y = player.getY();
		let tempArr = maze[y];
		let val = tempArr[x];
		if((val & direction) = direction){
			return true;
		}

		return false;
	}





}